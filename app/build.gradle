plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
}

android {
    def signingStorePath = System.getenv("SIGNING_KEY_STORE_PATH") ?: "keystore.jks"
    def signingStorePassword = System.getenv("SIGNING_STORE_PASSWORD")
    def signingKeyAlias = System.getenv("SIGNING_KEY_ALIAS")
    def signingKeyPassword = System.getenv("SIGNING_KEY_PASSWORD")
    def hasReleaseSigning = file(signingStorePath).exists() && signingStorePassword && signingKeyAlias && signingKeyPassword

    compileSdk 34

    defaultConfig {
        applicationId "me.stappmus.messagegateway"
        minSdk 21
        targetSdk 34
        versionCode 2
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    signingConfigs {
        release {
            if (hasReleaseSigning) {
                storeFile file(signingStorePath)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            manifestPlaceholders = [networkSecurityConfig: "@xml/network_security_config"]
        }
        debugInsecure {
            debuggable true
            manifestPlaceholders = [networkSecurityConfig: "@xml/network_security_config_insecure"]
            signingConfig signingConfigs.debug
        }
        insecure {
            initWith debug
            debuggable true
            signingConfig hasReleaseSigning ? signingConfigs.release : signingConfigs.debug
            manifestPlaceholders = [networkSecurityConfig: "@xml/network_security_config_insecure"]
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig hasReleaseSigning ? signingConfigs.release : signingConfigs.debug
            manifestPlaceholders = [networkSecurityConfig: "@xml/network_security_config"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }
    namespace 'me.stappmus.messagegateway'
}

dependencies {
    def ktor_version = "2.3.12"
    def okhttp_version = "4.12.0"
    def room_version = "2.6.1"
    def coroutines_version = "1.7.3"
    def work_version = "2.8.1"
    def koin_version = "3.5.6"
    def lifecycle_version = "2.6.2"

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version")

    implementation("io.ktor:ktor-serialization-gson:$ktor_version")

    implementation("io.ktor:ktor-server-core:$ktor_version")
    implementation("io.ktor:ktor-server-netty:$ktor_version")
    implementation("io.ktor:ktor-server-compression:$ktor_version")
    implementation("io.ktor:ktor-server-status-pages:$ktor_version")
    implementation("io.ktor:ktor-server-content-negotiation:$ktor_version")
    implementation("io.ktor:ktor-server-call-logging:$ktor_version")
    implementation("io.ktor:ktor-server-auth:$ktor_version")

    implementation("io.ktor:ktor-client-core:$ktor_version")
    implementation("io.ktor:ktor-client-content-negotiation:$ktor_version")
    implementation("io.ktor:ktor-client-okhttp:$ktor_version")

    implementation("com.squareup.okhttp3:okhttp:$okhttp_version")
    implementation("com.squareup.okhttp3:okhttp-sse:$okhttp_version")

    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation("androidx.preference:preference-ktx:1.2.1")

    implementation("androidx.core:core-ktx:1.12.0")
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("com.google.android.material:material:1.11.0")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")

    implementation("com.aventrix.jnanoid:jnanoid:2.0.0")
    implementation("com.klinkerapps:android-smsmms:5.2.6")
    runtimeOnly("org.slf4j:slf4j-nop:1.7.36")

    implementation("androidx.room:room-ktx:$room_version")
    annotationProcessor("androidx.room:room-compiler:$room_version")
    kapt("androidx.room:room-compiler:$room_version")

    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:32.7.4')
    implementation 'com.google.firebase:firebase-messaging-ktx'

    implementation 'com.googlecode.libphonenumber:libphonenumber:9.0.22'

    // Koin for Android
    implementation "io.insert-koin:koin-android:$koin_version"

    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.1")
}
